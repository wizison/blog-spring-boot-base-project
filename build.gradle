buildscript {
    ext {
        springBootVersion = '1.5.8.RELEASE'
        springDependencyManagementPluginVersion = '1.0.3.RELEASE'
        propDepsVersion = '0.0.9.RELEASE'
    }
    repositories {
        jcenter()
        maven {
            url 'http://repo.spring.io/plugins-release'
        }
    }
    dependencies {
        classpath("io.spring.gradle:dependency-management-plugin:${springDependencyManagementPluginVersion}")
        classpath("org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}")
        classpath("io.spring.gradle:propdeps-plugin:${propDepsVersion}")
    }
}

repositories {
    mavenLocal()
    jcenter()
}

apply plugin: 'java'
apply plugin: 'idea'
apply plugin: 'org.springframework.boot'
apply plugin: 'propdeps-idea'

sourceCompatibility = 1.8
targetCompatibility = 1.8

group 'at.wizi.blog'
version '1.0.0'

sourceSets {
    integrationTest {
        java {
            srcDir 'src/integrationTest/java'
        }
        resources {
            srcDir 'src/integrationTest/resources'
        }
        compileClasspath += sourceSets.test.compileClasspath
        runtimeClasspath += sourceSets.test.runtimeClasspath
    }
}

idea {
    module {
        downloadSources = true
        testSourceDirs += (sourceSets.integrationTest.java.srcDirs + sourceSets.integrationTest.resources.srcDirs)
    }
}

dependencyManagement {
    imports {
        mavenBom 'io.spring.platform:platform-bom:Brussels-SR7'
    }
}

dependencies {
    compile 'org.projectlombok:lombok'

    compile 'ch.qos.logback:logback-core'
    compile 'ch.qos.logback:logback-classic'

    compile 'org.springframework.boot:spring-boot-starter-web'

    optional 'org.springframework.boot:spring-boot-configuration-processor'

    testCompile 'org.assertj:assertj-core'
    testCompile 'org.springframework.boot:spring-boot-starter-test'

}

compileJava.dependsOn(processResources)

task integrationTest(type: Test) {
    group = 'verification'
    testClassesDir = sourceSets.integrationTest.output.classesDir
    classpath = sourceSets.integrationTest.runtimeClasspath
}

jar {
    baseName = 'spring-base'
    version = ''
}

springBoot {
    executable = true
    buildInfo()
}

task wrapper(type: Wrapper) {
    gradleVersion = '3.5.1'
}
